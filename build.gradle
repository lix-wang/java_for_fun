buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "http://repo.spring.io/milestone" }
        maven { url "http://maven.springframework.org/release" }
        maven { url "http://maven.springframework.org/milestone" }
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
    }
}

allprojects {
    group "com.xiao"
    version "1.0.0"
    apply from: rootProject.file("gradle/dependency.gradle")
}

// 子模块通用配置
subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'maven'

    buildDir = "out/output"

    // jvm 版本号要求
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "http://repo.spring.io/milestone" }
        maven { url "http://maven.springframework.org/release" }
        maven { url "http://maven.springframework.org/milestone" }
    }

    // idea 插件会默认下载source和doc文件
    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
            inheritOutputDirs = false
            outputDir = file("$buildDir/classes/main/")
        }
    }

    // 子模块添加依赖
    dependencies {
        testCompile libs['spring-boot-starter-test']
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok:1.18.4'
        annotationProcessor 'org.projectlombok:lombok:1.18.4'
        testCompileOnly 'org.projectlombok:lombok:1.18.4'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.4'
    }

    // 生成source源码包
    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
        classifier 'sources'
    }

//    artifacts {
//        archives sourceJar
//    }

    // 去掉默认的tomcat 和logging
    configurations {
        all*.exclude module: "spring-boot-starter-tomcat"
        all*.exclude module: "spring-boot-starter-logging"
    }
}

// 执行自定义gradle wrapper task 自动生成gradlew脚本及配置，这里指定下载的Gradle版本
task gradleWrapper(type: Wrapper) {
    gradleVersion = '4.0.1'
}